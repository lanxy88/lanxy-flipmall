<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
		 width="200" height="150" 
		 verticalScrollPolicy="off"
		 horizontalScrollPolicy="off"
		 clipContent="false"
		 horizontalGap="0"
		 horizontalAlign="center"
		 verticalAlign="bottom"
		 buttonMode="true" 
		 useHandCursor="true"
		 click="onClick(event)"
		 mouseDown="onMouseDown(event)"
		 mouseOver="onMouseOver(event)"
		 creationComplete="loadData()"
		 >
	<mx:Script>
		<![CDATA[
			import mx.containers.Canvas;
			import mx.controls.CheckBox;
			import mx.controls.Image;
			
			private var imgs:Array = [];
			
			[Bindable]
			public static var selectedItem:ThumbItem;
			
			private var _selected:Boolean = false;
			
			public function get selected():Boolean
			{
				return _selected;
			}
			
			[Bindable]
			public function set selected(value:Boolean):void
			{
				if(_selected == value) return;
				
				_selected = value;
				if(value == true)
				{
					if(selectedItem != null)
					{
						selectedItem.selected = false;
					}
					
					selectedItem = this;
					zoomOut.stop();
					zoomIn.play(imgs);
				}
				else
				{
					if(selectedItem == this)
					{
						selectedItem = null;
					}
					zoomIn.stop();
					zoomOut.play(imgs);
				}
			}
			
			[Bindable]
			public var pageHeight:int = 100;
			
			[Bindable]
			public var pageWidth:int = 70;
			
			public function pageInfo():String
			{
				var p0:int = (data as Array)[0].pageId;
				if((data as Array).length > 1)
				{
					var p1:int = (data as Array)[1].pageId;
					return p0.toString() + " - " + p1.toString();
				}
				else
				{
					return p0.toString();
				}
			}
			
			private function loadData():void
			{
				if(data==null) return;
				imgs = [];
				
				var array:Array = data as Array;
				
				var img0:Image = new Image();
				img0.addEventListener(Event.COMPLETE, function(e:*):void
				{
					var width:Number = img0.content.width;
					var height:Number = img0.content.height;
					img0.width = 70;
					img0.height = height * 70 / width;
					pageWidth = 70;
					pageHeight = img0.height;
				});
				img0.source = array[0].thumb;
				addChild(img0);
				imgs.push(img0);
				
				if(array.length > 1)
				{
					var img1:Image = new Image();
					img1.addEventListener(Event.COMPLETE, function(e:*):void
					{
						var width:Number = img1.content.width;
						var height:Number = img1.content.height;
						img1.width = 70;
						img1.height = height * 70 / width;
						pageWidth = 70;
						pageHeight = img0.height;
					});
					img1.source = array[1].thumb;
					addChild(img1);
					imgs.push(img1);
				}
			}
			
			private function onClick(event:MouseEvent):void
			{
				var page:int = int((data as Array)[0].pageId);
				RunTime.mainPage.gotoPage(page);
				RunTime.mainPage.shiftLeft();
			}
			
			private function onMouseOver(event:MouseEvent):void
			{
				selected = true;
			}
			
			private function onMouseDown(event:MouseEvent):void
			{
				event.stopPropagation();
			}
		]]>
	</mx:Script>
	<mx:Resize id="zoomIn" widthFrom="{pageWidth}" widthTo="{pageWidth*1.3}" heightFrom="{pageHeight}" heightTo="{pageHeight*1.3}" />
	<mx:Resize id="zoomOut" widthFrom="{pageWidth*1.3}" widthTo="{pageWidth}" heightFrom="{pageHeight*1.3}" heightTo="{pageHeight}" />
</mx:HBox>
